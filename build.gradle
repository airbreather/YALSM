defaultTasks 'build'

buildscript
{
    repositories
    {
        mavenCentral()

        maven
        {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }

        maven
        {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

// define the properties file
ext.configFile = file 'build.properties'

configFile.withReader
{
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

archivesBaseName = 'yalsm'
version = config.mod_version

minecraft.version = config.minecraft_version + '-' + config.forge_version // grab latest forge
minecraft.runDir = 'eclipse'

processResources
{
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs)
    {
        include '**/*.info'
        include '**/*.properties'

        // replaces
        expand(['mod_version': config.mod_version,
                'minecraft_version': config.minecraft_version,
                'forge_version': config.forge_version])
    }

    // copy everything else (what we didn't do before)
    from(sourceSets.main.resources.srcDirs)
    {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

// Exclude the core from the package.
// I should start keeping these separate, because updates can screw with things.
jar.exclude '**/airbreathercore/**'