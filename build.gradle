defaultTasks 'setupDevWorkspace', 'build'

buildscript
{
    repositories
    {
        jcenter()

        maven
        {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

// define the properties file
ext.configFile = file 'build.properties'

configFile.withReader
{
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

archivesBaseName = 'yalsm'
version = config.mod_version
group = 'airbreather.mods.yalsm'

minecraft.version = config.minecraft_version + '-' + config.forge_version
minecraft.runDir = 'eclipse'
minecraft.mappings = 'snapshot_20160518'
minecraft.makeObfSourceJar = false

processResources
{
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs)
    {
        include '**/*.info'
        include '**/*.properties'

        // replaces
        expand(['mod_version': config.mod_version,
                'minecraft_version': config.minecraft_version,
                'forge_version': config.forge_version])
    }

    // copy everything else (what we didn't do before)
    from(sourceSets.main.resources.srcDirs)
    {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar.exclude 'airbreather/mods/airbreathercore'
